plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.61'
    id 'application'
    id("com.github.johnrengelman.shadow") version "5.0.0"
    id 'java'
    id 'idea'
    id 'jacoco'
    id "com.jfrog.bintray" version "1.8.4"
    id "com.diffplug.gradle.spotless" version "3.25.0"
    id 'io.codearte.nexus-staging' version '0.21.1'
    id "de.marcphilipp.nexus-publish" version "0.4.0"
    id "de.undercouch.download" version "4.0.0"
}

apply plugin: 'kotlin-kapt'
apply {
    [
        'bintray',
        'jacoco',
        'java',
        'javadoc',
        'repositories',
        'publish',
        'spotless',
        'junit'
    ].each { buildScript ->
        download {
            src "https://raw.githubusercontent.com/web3j/build-tools/master/gradle/$buildScript/build.gradle"
            dest "$rootDir/gradle/$buildScript/build.gradle"
            overwrite true
            quiet true
            onlyIfModified true
        }
        from("$rootDir/gradle/$buildScript/build.gradle")
    }
}
repositories {
    mavenCentral()
    jcenter()
    maven { url 'https://kotlin.bintray.com/ktor' }
}

ext.ktor_version = '1.3.2'

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    implementation platform('org.jetbrains.kotlin:kotlin-bom')
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'

    implementation 'com.beust:klaxon:5.0.1'
    compile 'info.picocli:picocli:4.2.0'

    implementation "io.ktor:ktor-server-netty:$ktor_version"
    implementation "io.ktor:ktor-server-core:$ktor_version"
    implementation "io.ktor:ktor-html-builder:$ktor_version"
    implementation "io.ktor:ktor-client-core:$ktor_version"
    implementation "io.ktor:ktor-client-core-jvm:$ktor_version"
    implementation "io.ktor:ktor-client-apache:$ktor_version"
    implementation "io.ktor:ktor-server-test-host:$ktor_version"

    implementation "org.web3j:web3j-evm:4.5.16"
    implementation "org.web3j:core:4.5.16"

    testImplementation 'org.jetbrains.kotlin:kotlin-test'
    testImplementation 'org.jetbrains.kotlin:kotlin-test-junit'

    testImplementation "io.ktor:ktor-server-tests:$ktor_version"
    testImplementation "io.ktor:ktor-server-test-host:$ktor_version"
    testImplementation "io.ktor:ktor-client-mock:$ktor_version"
    testImplementation "io.ktor:ktor-client-mock-jvm:$ktor_version"
    testCompile "io.rest-assured:rest-assured:3.3.0"
}

application {
    mainClassName = 'com.epirus.local.MainKt'
}

jar {
    from {
        configurations.compile.grep {
            def isBesuLib = it.getParentFile().getName().equals("libs") && (it.getName().startsWith("besu-") || it.getName().startsWith("plugin-"))

            return isBesuLib
        }
        .collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

run {
    standardInput = System.in
}
